plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'java'
    id 'application'
    id 'idea'
    id 'eclipse'
    id 'com.palantir.docker' version '0.25.0'
    id 'com.palantir.docker-run' version '0.25.0'
}

repositories {
    mavenCentral()
}

project.version = '0.1.1'
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

dependencies {
    implementation platform('com.amazonaws:aws-java-sdk-bom:1.11.+')
    implementation 'com.amazonaws:aws-java-sdk-ec2'
    implementation 'org.slf4j:slf4j-api:1.7.+'
    implementation 'ch.qos.logback:logback-core:1.2.+'
    implementation 'ch.qos.logback:logback-classic:1.2.+'
    implementation 'net.logstash.logback:logstash-logback-encoder:6.4'

    testImplementation 'junit:junit:4.13'
}

application {
    mainClassName = 'info.akotadakura.App'
}

tasks.withType(JavaCompile).all {
    options.encoding = "UTF-8"
}

jar {
    archiveFileName = "${project.name}.jar"
    manifest {
        attributes 'Main-Class' : 'info.akotadakura.App'
    }
}

shadowJar {
    archiveFileName = "${project.name}.jar"
}

docker {
    name "${project.name}:${project.version}"
    dockerfile file('Dockerfile')
    copySpec.from('build/libs/').into('build/libs')
    noCache true
}
docker.dependsOn(shadowJar)

dockerRun {
    name project.name
    image "${project.name}:${project.version}"
    daemonize false
    clean true
}